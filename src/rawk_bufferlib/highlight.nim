import std/[os, strutils, sets, tables]
import rawk_luigi, font

type
  TokenKind* = enum
    tkDefault, tkKeyword, tkString, tkComment, tkNumber, tkOperator,
    tkProcName, tkTypeName, tkReturnType

  LangKind* = enum lkGeneric, lkNim, lkDiff, lkMarkdown

  Span* = object
    col*, n*: int
    kind*: TokenKind

  SyntaxRule* = object
    name*: string
    lang*: LangKind
    extensions*: seq[string]
    keywords*: HashSet[string]
    defKeywords*: seq[string]
      ## Declaration keywords for goto-definition (conf key `def_keywords`),
      ## e.g. `proc func type` for nim. A consumer greps for one of these
      ## followed by the symbol. Ordered seq (not a set) so the host can join
      ## it straight into a regex alternation. Empty when the language has no
      ## keyword-prefixed declarations (or none was configured).
    commentLine*: string
    commentOpen*, commentClose*: string
    stringDelims*: set[char]
    operators*: set[char]

  ExtraTheme* = object
    ## Color slots luigi's UITheme doesn't expose. The host populates these
    ## whenever it swaps palettes and calls `setHighlightTheme`. `accent` is
    ## used by the editor for focus borders and the cursor underline; the
    ## rest are for syntax highlighting.
    codeKeyword*, codeType*, codeReturnType*, urgent*, accent*: uint32

const nimProcKeywords = ["proc", "func", "iterator", "template", "macro",
                         "method", "converter"].toHashSet

template embedSyntax(n: untyped): (string, string) =
  (astToStr(n), staticRead("syntax/" & astToStr(n) & ".conf"))

const builtinSyntaxes*: array[7, (string, string)] = [
  embedSyntax(nim),
  embedSyntax(c),
  embedSyntax(cpp),
  embedSyntax(python),
  embedSyntax(js),
  embedSyntax(diff),
  embedSyntax(markdown),
]

var
  rules: seq[SyntaxRule]
  byExt: Table[string, int]   # extension -> index into rules
  extraTheme*: ExtraTheme     # set by setHighlightTheme; read by editor too

proc setHighlightTheme*(t: ExtraTheme) =
  ## Call after the host swaps palettes so the four luigi-less color slots
  ## (codeKeyword / codeType / codeReturnType / urgent) follow along.
  extraTheme = t

proc parseRule(name, body: string): SyntaxRule =
  result.name = name
  if name == "nim": result.lang = lkNim
  elif name == "diff": result.lang = lkDiff
  elif name == "markdown": result.lang = lkMarkdown
  for raw in body.splitLines():
    let line = raw.strip()
    if line.len == 0 or line.startsWith('#'): continue
    let colon = line.find(':')
    if colon <= 0: continue
    let key = line[0 ..< colon].strip()
    let val = line[colon+1 .. ^1].strip()
    case key
    of "extensions":
      for raw in val.split(','):
        let s = raw.strip().toLowerAscii()
        if s.len > 0: result.extensions.add(s)
    of "comment_line":        result.commentLine  = val
    of "comment_block_open":  result.commentOpen  = val
    of "comment_block_close": result.commentClose = val
    of "string_delims":
      for c in val: result.stringDelims.incl(c)
    of "operators":
      for c in val: result.operators.incl(c)
    of "keywords":
      for kw in val.split({' ', '\t'}):
        if kw.len > 0: result.keywords.incl(kw)
    of "def_keywords":
      for kw in val.split({' ', '\t'}):
        if kw.len > 0: result.defKeywords.add(kw)
    else: discard            # scope_* keys reserved for future tmTheme loader

proc loadAllSyntaxes*() =
  rules.setLen(0)
  byExt.clear()
  for (n, body) in builtinSyntaxes:
    rules.add(parseRule(n, body))
    let i = rules.len - 1
    for ext in rules[i].extensions:
      byExt[ext] = i

proc syntaxForPath*(path: string): ptr SyntaxRule =
  if path.len == 0 or rules.len == 0: return nil
  let ext = splitFile(path).ext.toLowerAscii.strip(chars = {'.'})
  if ext.len == 0: return nil
  if not byExt.hasKey(ext): return nil
  return addr rules[byExt[ext]]

proc syntaxByName*(name: string): ptr SyntaxRule =
  for i in 0 ..< rules.len:
    if rules[i].name == name: return addr rules[i]
  nil

proc isIdentStart(c: char): bool {.inline.} = c.isAlphaAscii or c == '_'
proc isIdentCont(c: char): bool {.inline.} = c.isAlphaNumeric or c == '_'

proc matchesAt(line: string, i: int, s: string): bool {.inline.} =
  if s.len == 0 or i + s.len > line.len: return false
  for k in 0 ..< s.len:
    if line[i + k] != s[k]: return false
  return true

proc tokenizeLine*(line: string, rule: ptr SyntaxRule,
                   prevState: uint8, spans: var seq[Span]): uint8 =
  ## Tokenizes one line. `prevState` 1 = inside a block comment from the
  ## previous line. Returns the trailing state for the next line.
  spans.setLen(0)
  if rule == nil:
    if line.len > 0:
      spans.add(Span(col: 0, n: line.len, kind: tkDefault))
    return 0

  if rule.lang == lkMarkdown:
    # State: 2 = inside a ``` fenced code block carried over from previous line.
    # A code-fence line toggles state; while inside, the whole line paints as
    # tkString. Otherwise: heading lines (#…), blockquotes (>…), and inline
    # `…` code spans get classified; the rest is default.
    let inFence = (prevState == 2'u8)
    let trimmed = line.strip(leading = true, trailing = false)
    let isFence = trimmed.startsWith("```") or trimmed.startsWith("~~~")
    if isFence:
      if line.len > 0:
        spans.add(Span(col: 0, n: line.len, kind: tkString))
      return (if inFence: 0'u8 else: 2'u8)
    if inFence:
      if line.len > 0:
        spans.add(Span(col: 0, n: line.len, kind: tkString))
      return 2'u8
    if trimmed.startsWith("#"):
      spans.add(Span(col: 0, n: line.len, kind: tkProcName))
      return 0'u8
    if trimmed.startsWith(">"):
      spans.add(Span(col: 0, n: line.len, kind: tkComment))
      return 0'u8
    # Inline backtick code spans.
    var i = 0
    while i < line.len:
      if line[i] == '`':
        let start = i
        inc i
        while i < line.len and line[i] != '`': inc i
        if i < line.len: inc i   # consume closing backtick
        spans.add(Span(col: start, n: i - start, kind: tkString))
      else:
        inc i
    return 0'u8

  if rule.lang == lkDiff:
    if line.len == 0: return 0
    let kind =
      if line.startsWith("+++") or line.startsWith("---"):
        tkComment
      elif line.startsWith("## ") or line.startsWith("@@"):
        tkNumber
      elif line.startsWith("diff --git") or line.startsWith("index ") or
           line.startsWith("new file") or line.startsWith("deleted file") or
           line.startsWith("similarity ") or line.startsWith("rename "):
        tkComment
      else:
        # +/- and context lines render in the standard fg colour. The row
        # gets a gentle red/green background instead — see lineBgColor.
        tkDefault
    spans.add(Span(col: 0, n: line.len, kind: kind))
    return 0

  var i = 0
  let n = line.len

  # --- continuation of a block comment from the previous line
  if prevState == 1'u8:
    if n == 0: return 1'u8
    let close = rule.commentClose
    var j = 0
    var closed = false
    while j < n:
      if close.len > 0 and matchesAt(line, j, close):
        j += close.len
        closed = true
        break
      inc j
    spans.add(Span(col: 0, n: j, kind: tkComment))
    i = j
    if not closed:
      return 1'u8

  # Per-line Nim-aware state. Tracks "next ident should be colored as X" and
  # whether we're inside a proc-def line (so `:` triggers return-type coloring
  # for both arg types in the params and the return type after `):`).
  var pendingKind = tkDefault
  var inProcDef = false

  while i < n:
    let c = line[i]

    # block comment open
    if rule.commentOpen.len > 0 and matchesAt(line, i, rule.commentOpen):
      let start = i
      var j = i + rule.commentOpen.len
      var closed = false
      while j < n:
        if rule.commentClose.len > 0 and matchesAt(line, j, rule.commentClose):
          j += rule.commentClose.len
          closed = true
          break
        inc j
      spans.add(Span(col: start, n: j - start, kind: tkComment))
      i = j
      if not closed:
        return 1'u8
      continue

    # line comment
    if rule.commentLine.len > 0 and matchesAt(line, i, rule.commentLine):
      spans.add(Span(col: i, n: n - i, kind: tkComment))
      i = n
      continue

    # string
    if c in rule.stringDelims:
      let delim = c
      let start = i
      inc i
      while i < n:
        if line[i] == '\\' and i + 1 < n:
          i += 2
          continue
        if line[i] == delim:
          inc i
          break
        inc i
      spans.add(Span(col: start, n: i - start, kind: tkString))
      continue

    # number (only if not in middle of identifier)
    if c.isDigit:
      let start = i
      inc i
      while i < n:
        let ch = line[i]
        if ch.isAlphaNumeric or ch == '.' or ch == '_': inc i
        else: break
      spans.add(Span(col: start, n: i - start, kind: tkNumber))
      continue

    # identifier / keyword
    if isIdentStart(c):
      let start = i
      inc i
      while i < n and isIdentCont(line[i]): inc i
      let word = line[start ..< i]
      if rule.keywords.contains(word):
        spans.add(Span(col: start, n: i - start, kind: tkKeyword))
        if rule.lang == lkNim:
          if word in nimProcKeywords:
            inProcDef = true
            pendingKind = tkProcName
          elif word == "type":
            pendingKind = tkTypeName
      elif pendingKind != tkDefault:
        spans.add(Span(col: start, n: i - start, kind: pendingKind))
        pendingKind = tkDefault
      # else: leave as default (no span needed; gap between spans paints default)
      continue

    # operator (single char)
    if c in rule.operators:
      spans.add(Span(col: i, n: 1, kind: tkOperator))
      if rule.lang == lkNim and inProcDef and c == ':':
        pendingKind = tkReturnType
      inc i
      continue

    # default: skip
    inc i

  return 0'u8

proc mixColor(a, b: uint32, alpha: float): uint32 {.inline.} =
  let inv = 1.0 - alpha
  let ar = int((a shr 16) and 0xFF); let br = int((b shr 16) and 0xFF)
  let aG = int((a shr 8)  and 0xFF); let bG = int((b shr 8)  and 0xFF)
  let aB = int(a and 0xFF);          let bB = int(b and 0xFF)
  let nr = int(float(ar) * alpha + float(br) * inv)
  let ng = int(float(aG) * alpha + float(bG) * inv)
  let nb = int(float(aB) * alpha + float(bB) * inv)
  uint32((nr shl 16) or (ng shl 8) or nb)

proc lineBgColor*(rule: ptr SyntaxRule, line: string): uint32 =
  ## 0 = no row background tint. Diff `+` / `-` lines get a gentle green /
  ## red blended toward the editor's code background. File / hunk header
  ## lines stay untinted so the eye can find section boundaries.
  if rule == nil or rule.lang != lkDiff or line.len == 0: return 0
  if line.startsWith("+++") or line.startsWith("---"): return 0
  let bg = ui.theme.codeBackground
  if line[0] == '+':
    return mixColor(extraTheme.codeType, bg, 0.18)
  if line[0] == '-':
    return mixColor(extraTheme.urgent, bg, 0.20)
  0

proc colorFor*(kind: TokenKind): uint32 {.inline.} =
  case kind
  of tkKeyword:    ui.theme.codePreprocessor   # luigi has no codeKeyword slot
  of tkString:     ui.theme.codeString
  of tkComment:    ui.theme.codeComment
  of tkNumber:     ui.theme.codeNumber
  of tkOperator:   ui.theme.codeOperator
  of tkProcName:   extraTheme.codeKeyword
  of tkTypeName:   extraTheme.codeType
  of tkReturnType: extraTheme.codeReturnType
  of tkDefault:    ui.theme.codeDefault

proc paintLine*(painter: ptr Painter, r: Rectangle, line: string,
                rule: ptr SyntaxRule, prevState: uint8,
                spans: var seq[Span]) =
  ## Paints one line with the supplied tokenizer state. Caller should pass a
  ## reusable spans buffer to avoid per-call allocations in steady state.
  discard tokenizeLine(line, rule, prevState, spans)

  let (gW, _) = glyphDims()
  let bx = r.l

  # Pass 1: background-fill any gap with default color (only spans we kept
  # above are non-default tokens; gaps are default).
  var col = 0
  for s in spans:
    if s.col > col:
      let txt = line[col ..< s.col]
      let rect = Rectangle(l: bx + cint(col) * gW, r: r.r, t: r.t, b: r.b)
      drawString(painter, rect, txt.cstring, txt.len,
                 ui.theme.codeDefault, cint(ALIGN_LEFT), nil)
    let txt = line[s.col ..< s.col + s.n]
    let rect = Rectangle(l: bx + cint(s.col) * gW, r: r.r, t: r.t, b: r.b)
    drawString(painter, rect, txt.cstring, txt.len,
               colorFor(s.kind), cint(ALIGN_LEFT), nil)
    col = s.col + s.n
  if col < line.len:
    let txt = line[col ..< line.len]
    let rect = Rectangle(l: bx + cint(col) * gW, r: r.r, t: r.t, b: r.b)
    drawString(painter, rect, txt.cstring, txt.len,
               ui.theme.codeDefault, cint(ALIGN_LEFT), nil)

proc advanceState*(line: string, rule: ptr SyntaxRule,
                   prevState: uint8): uint8 =
  ## Cheap variant for the editor's lineStartStates cache: tokenize but
  ## discard spans, just return the trailing state.
  var tmp: seq[Span]
  return tokenizeLine(line, rule, prevState, tmp)
